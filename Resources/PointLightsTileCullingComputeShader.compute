#pragma kernel PointLightTileCulling

#include "Packages/com.barkar.bsrp/ShaderLibrary/Common.hlsl"
#include "Packages/com.barkar.bsrp/ShaderLibrary/UnityInput.hlsl"

Texture2D<float> _DepthTexture : register(t1);
RWStructuredBuffer<int> _TileLightCountBuffer : register(u0);
RWStructuredBuffer<int> _TileLightIndicesBuffer : register(u1);

groupshared int minDepthShared;
groupshared int maxDepthShared;

groupshared uint tileVisibleLightIndices[MAX_LIGHT_COUNT];
groupshared uint tileVisibleLightCount = 0;

bool Intersects(float4 positionView, float radius, float4 frustumPlanes[6])
{
    bool inFrustum = true;
    [unroll] for (int i = 0; i < 6; ++i)
    {
        float d = dot(frustumPlanes[i], float4(positionView.xyz, 1.0f));
        inFrustum = inFrustum && (d >= -radius);
    }
    return inFrustum;
}


[numthreads(TILESIZE, TILESIZE, 1)]
void PointLightTileCulling(uint3 threadID : SV_GroupThreadID, uint3 groupID : SV_GroupID, uint groupIdx : SV_GroupIndex)
{
    uint2 textureSize;
    _DepthTexture.GetDimensions(textureSize.x, textureSize.y);

    float2 uv = (float2(groupID.xy * TILESIZE + threadID.xy) + 0.5f) / textureSize;
    float depth = _DepthTexture.SampleLevel(sampler_point_clamp, uv, 0).r;
    depth = LinearEyeDepth(depth, _ZBufferParams);
    depth = asuint(depth);
    
    if (threadID.x == 0 && threadID.y == 0) {
        minDepthShared = 0;
        maxDepthShared = 0;
    }
    GroupMemoryBarrierWithGroupSync();
    
    InterlockedMin(minDepthShared, depth);
    InterlockedMax(maxDepthShared, depth);
    GroupMemoryBarrierWithGroupSync();
    
    float minDepth = asfloat(minDepthShared);
    float maxDepth = asfloat(maxDepthShared);

    float2 tileScale = textureSize * rcp(float(2 * TILESIZE));
    float2 tileBias = tileScale - float2(groupID.xy);

    float4 c1 = float4(UNITY_MATRIX_P._m00 * tileScale.x, 0.0f, -tileBias.x, 0.0f);
    float4 c2 = float4(0.0f, -UNITY_MATRIX_P._m11 * tileScale.y, -tileBias.y, 0.0f);
    float4 c4 = float4(0.0f, 0.0f, -1.0f, 0.0f);
    
    float4 frustumPlanes[6];
    frustumPlanes[0] = c4 - c1; // right
    frustumPlanes[1] = c1; // left
    frustumPlanes[2] = c4 - c2; // top
    frustumPlanes[3] = c2; // bottom
    frustumPlanes[4] = float4(0.0f, 0.0f, 1.0f, maxDepth); //far
    frustumPlanes[5] = float4(0.0f, 0.0f, -1.0f, -minDepth); // near

    [unroll] for (uint p = 0; p < 4; ++p)
    {
        frustumPlanes[p] *= rcp(length(frustumPlanes[p].xyz));
    }
    
    for (int lightIndex = groupIdx; lightIndex < PointLightCount; lightIndex += TILE_GROUP_SIZE)
    {
        float r = PointLightColors[lightIndex].w;
        float4 positionView = mul(UNITY_MATRIX_V, float4(PointLightPositionsAndRadius[lightIndex].xyz, 1.0));
        positionView.xyz /= positionView.w;

        bool intersect = Intersects(positionView, r, frustumPlanes);
        if (intersect )
        {
            uint offset;
            InterlockedAdd(tileVisibleLightCount, 1, offset);
            tileVisibleLightIndices[offset] = lightIndex;
        }
    }

    int tileIndex = groupID.y * (textureSize.x / TILESIZE) + groupID.x;

    if (groupIdx == 0) {
        _TileLightCountBuffer[tileIndex] = tileVisibleLightCount;
    }

    for (uint i = groupIdx; i < tileVisibleLightCount && i < PER_TILE_LIGHT_COUNT; i += TILE_GROUP_SIZE)
    {
        _TileLightIndicesBuffer[tileIndex * PER_TILE_LIGHT_COUNT + i] = tileVisibleLightIndices[i];
    }
    
}



